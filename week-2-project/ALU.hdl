// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
        Mux16 (a=x, b=false, sel=zx, out=temp1);
        Mux16 (a=y, b=false, sel=zy, out=temp2);

        Not16 (in=temp1, out=temp3);
        Not16 (in=temp2, out=temp4);

        Mux16 (a=temp1, b=temp3, sel=nx, out=temp5);
        Mux16 (a=temp2, b=temp4, sel=ny, out=temp6);

        Add16 (a=temp5, b=temp6, out=temp7);
        And16 (a=temp5, b=temp6, out=temp8);

        Mux16 (a=temp8, b=temp7, sel=f, out=temp9);
        Not16 (in=temp9, out=temp10);

        Mux16 (a=temp9, b=temp10, sel=no,  out=out, out[15]=ng, out[0..7]=abcdef, out[8..15]=secondHalfOut);

        Or8Way (in=abcdef, out=abcd);
        Or8Way (in=secondHalfOut, out=out12);
        Or (a=abcd, b=out12, out=qwerty);
        Not (in=qwerty, out=zr);
}
